% crochet

open purr.ide.ui;

type application-configure(project is purr-project) is purr-application;

command application-configure title = "Configure";
command application-configure icon = #widget icon: "cog";
command application-configure description = "View and update the configuration of your project";

enum application-configure-pages =
  meta-data,
  capabilities;

command application-configure render-application do
  #widget tabbed-panel: [
    #tab id: "meta-data"
      | header: "Meta-data"
      | content: self meta-data,

    #tab id: "capabilities"
      | header: "Capabilities"
      | content: self capabilities,
  ]
  | layout: #tabbed-panel-layout vertical-left
  | selected: "meta-data";
end

command application-configure title: Title actions: Actions body: Body do
  #widget flex-column: [
    #widget flex-row: [
      pui heading-2: Title,
      Actions,
    ]
    | transform
    | flex-align-items: #flex-align baseline
    | flex-justify-content: #flex-justify space-between,
    Body,
  ]
  | transform
  | flex-gap: #text-size base
  | padding-horizontal: #text-size large-2x
  | padding-vertical: #text-size base
  | height: #size fit-content; 
end

command application-configure meta-data do
  let Name = #reference text-input: "name";
  let Title = #reference text-input: "title";
  let Description = #reference text-input: "description";
  let Project = purr current-project;

  self
    title: "Meta-data"
    actions: #widget space
    body: (
      #widget flex-column: [
        #widget text-input: "Name"
          | value: (Project map: { X in X meta name })
          | placeholder: "domain.you.your.project"
          | reference: Name,

        #widget text-input: "Title"
          | value: (Project map: { X in X meta title })
          | placeholder: "Your Great Project"
          | reference: Title,

        #widget text-input: "Description"
          | value: (Project map: { X in X meta description})
          | placeholder: "Does this and that and that other thing"
          | reference: Description,
      ]
      | transform
      | flex-gap: #text-size base
    );
end


command application-configure capabilities do
  self
    title: "Capabilities"
    actions: #widget space
    body: (
      #widget tabbed-panel: [
        #tab id: "requested"
          | header: "Requested"
          | content: self requested-capabilities,
        
        #tab id: "provided"
          | header: "Provided"
          | content: self provided-capabilities,
      ]
      | selected: "requested"
    );
end

command application-configure requested-capabilities do
  let Capabilities = purr current-project map: { X in X meta capabilities };
  let Required = Capabilities map: { X in X strictly-required };
  let Optional = Capabilities map: { X in X optional };
  let Trusted = Capabilities map: { X in X trusted };

  #widget flex-column: [
    self capability-section: "Required"
        description: "The package needs these capabilities to run."
        items: Required,

    self capability-section: "Optional"
        description: "The package can use these capabilities to provide additional features."
        items: Optional,

    self capability-section: "Trusted"
        description: "The package will be granted these capabilities automatically when
                      included as part of Crochet's Trusted Computing Base (standard library)."
        items: Trusted,
  ]
  | transform
  | padding-vertical: (1.0 as root-em);
end

command application-configure capability-section: Title description: Desc items: Items-cell do
  Items-cell map: { Items in 
    condition
      when Items is-empty => [];
      otherwise =>
        #widget card: [
          #card-child header: (
            #widget flex-column: [
              Title,
              (Desc as widget)
                | transform
                | font-size: #text-size small
            ]
          ),
          #card-child body: (
            #widget flex-column: (Items map: (pui requested-capability: _))
              | flex-gap: (1.0 as root-em)
          )
        ]
        | style: #card-style fluid
        | transform
        | margin-bottom: (2.0 as root-em);
    end
  };
end

command application-configure provided-capabilities do
  "";
end