%pickles/1

module package

let Package = {
  name: Text at name,
  title: (Text at title) ?? "",
  description: (Text at title) ?? "",
  target: Target at target,
  stability: (Stability at stability) ?? "unknown",
  capabilities: Capabilities at capabilities,
};

let Target =
  match Text with
    when "*" => constant "any";
    when "any" => constant "any";
    when "browser" => constant "browser";
    when "node" => constant "node";
  end;

let Stability =
  "experimental"
| "stable"
| "frozen"
| "deprecated"
| constant "unknown";

let Capabilities = {
  requested: [
    ...((List(Requested-capability(constant "required")) at requires) | []),
    ...((List(Requested-capability(constant "optional")) at optional) | []),
    ...((List(Requested-capability(constant "trusted")) at trusted) | [])
  ],
  provided: (List(Provided-capability) at provides) | [],
};

let Requested-capability(Kind) = 
  {
    name: Text at name,
    reason: (Text at reason) ?? "",
    kind: Kind,
  }
| {
    name: Text,
    reason: constant "",
    kind: Kind,
  };

let Provided-capability =
  {
    name: Text at name,
    title: (Text at title) ?? "",
    description: (Text at description) ?? "",
    risk: (Capability-risk at risk) ?? "unknown",
  }
| {
    name: Text,
    title: Text,
    description: constant "",
    risk: constant "unknown",
  };

let Capability-risk =
  "low" | "medium" | "high" | "critical" | constant "unknown";