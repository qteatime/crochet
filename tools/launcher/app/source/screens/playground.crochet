% crochet

open crochet.debug;

local define w = agata-widget;
local define cw = purr-common-widgets;

local abstract state;
local singleton loading is state;
local type running(process is pp-playground) is state;

effect playground with
  set-state(state is state);
  get-state();
  get-project();
end

local type entry(state is observable<pstate>);

local abstract pstate;
local singleton ps-idle is pstate;
local type ps-running(sid is text, code is text) is pstate;
local type ps-error(code is text, message is text) is pstate;

effect playground-entry with
  set-state(state is pstate);
  run(code is text);
  commit(code is text);
  rollback();
end

effect playground-repl with
  add-entry();
end



command ps-playground render do
  let State = #observable with-value: loading;

  handle
    w commit: (
      cw project-window: self.project at: "playground" with: [
        w title: "Crochet Playground",
        w space,

        w section: [State]
      ]
    );
  with
    on playground.get-project() do
      continue with self.project;
    end

    on playground.get-state() do
      continue with State value;
    end

    on playground.set-state(New-state) do
      State <- New-state;
      continue with nothing;
    end
  end
end

implement to-widget for state;
command loading as widget do
  transcript write: "Loading to-widget tap";
  transcript inspect: (runtime-debugger current-activation handlers to-text);

  purr defer: {
    let Project = perform playground.get-project();
    let Process = purr spawn-playground: Project;
    perform playground.set-state(new running(Process));
  };

  "Loading...";
end

command running as widget do
  let Entry = new entry(#observable with-value: ps-idle);
  let Entries = #observable-stream with-values: [Entry];

  handle
    w commit: Entries;
  with
    on playground-repl.add-entry() do
      Entries publish: new entry(#observable with-value: ps-idle);
      continue with nothing;
    end
  end
end

implement to-widget for entry;
command entry as widget do
  w with-progress-root: { Progress in 
    handle
      w commit: self.state
    with
      on playground-entry.set-state(New-state) do
        self.state <- New-state;
        continue with nothing;
      end

      on playground-entry.run(Code) do
        Progress show: "Running...";
        let State = perform playground.get-state();
        let Project = perform playground.get-project();
        let Sid = purr project: Project run-snippet: Code in: State.process;
        self.state <- new ps-running(Sid, Code);
        continue with nothing;
      end

      on playground-entry.commit(Code) do
        Progress hide;
        continue with nothing;
      end

      on playground-entry.rollback() do
        Progress hide;
        continue with nothing;
      end
    end
  }
end


implement to-widget for pstate;
command ps-idle as widget do
  let Editor = agata reference: "editor";

  w style: "launcher-playground-entry" with: [
    w code-editor
      | on-commit: { perform playground-entry.run(Editor materialisation current-value) }
      | reference: Editor
      | focus,

    w icon-button: "play"
      | title: "Run this code snippet"
      | on-click: { _ in
          perform playground-entry.run(Editor materialisation current-value);
        }
  ];
end

command ps-running as widget do 
  let State = perform playground.get-state();
  let Logs = State.process.events as observable-stream
               | filter: { X in X is-log-event and (X.id === self.sid) };
  let Values = State.process.events as observable-stream
                 | filter: { X in (X is pge-success) and (X.id === self.sid) };
  
  State.process.events subscribe: { X in
    condition
      when (X.id === self.sid) and (X is pge-success) do
        perform playground-entry.commit(self.code);
        perform playground-repl.add-entry();
      end

      when (X.id === self.sid) and (X is pge-error) do
        perform playground-entry.rollback();
        perform playground-entry.set-state(new ps-error(self.code, X.message));
      end

      otherwise do nothing end
    end
  };

  w style: "launcher-playground-entry" with: [
    w code-editor
      | read-only
      | code: self.code,

    w style: "launcher-playground-entry-results" with: [
      w tabbed-panel: [
        w tab: "Result"
          | with: [w contents: [w style: "launcher-playground-entry-values" with: [Values]]],

        w tab: "Logs"
          | with: [w contents: [w style: "launcher-playground-entry-logs" with: [Logs]]]
      ]
    ]
  ];
end

command ps-error as widget do
  let Editor = agata reference: "editor";

  w style: "launcher-playground-entry" with: [
    w code-editor
      | code: self.code
      | on-commit: { perform playground-entry.run(Editor materialisation current-value) }
      | reference: Editor
      | focus,

    w icon-button: "play"
      | title: "Run this code snippet"
      | on-click: { _ in
          perform playground-entry.run(Editor materialisation current-value);
        },

    w style: "launcher-playground-entry-errors" with: [
      w typewriter-text: [self.message]
    ]
  ];
end



command playground-event is-log-event = false;
command pge-declaration-loaded is-log-event = true;

implement to-widget for pge-declaration-loaded;
command pge-declaration-loaded as widget do
  w style: "launcher-playground-entry-log-entry" with: [
    self.message
  ]
end

implement to-widget for pge-success;
command pge-success as widget do
  w style: "launcher-playground-entry-value" with: [
    self.result
  ]
end


