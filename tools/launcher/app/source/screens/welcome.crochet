% crochet

local define w = agata-widget;
local define cw = purr-common-widgets;

command ps-welcome render do
  cw window: [] with: [
    w tabbed-panel: [
      w tab: "My projects"
        | with: [self my-projects],
      w tab: "Examples"
        | with: [self examples],
      w tab: "Libraries"
        | with: [self libraries],
    ]
  ]
end

command ps-welcome my-projects do
  w contents: [
    w highlight-header: [
      w title: ["You don't have any projects yet."],
      w subtitle: ["Create or import a project to start working"],
    ],

    w action-list: [
      w action-button
        | icon: "plus"
        | title: "New project"
        | description: "Create a new project based on a template"
        | on-click: { _ in purr new-project },

      w action-button
        | icon: "file-import"
        | title: "Import a project"
        | description: "Add an existing project to the launcher"
        | on-click: { _ in purr import-project }
    ]
  ]
end

command ps-welcome examples do
  let Projects = purr-api examples;
  w contents: [
    self projects: Projects
  ];
end

command ps-welcome libraries do
  let Projects = purr-api libraries;
  w contents: [
    self projects: Projects
  ];
end

command ps-welcome projects: (Projects is list<purr-project-meta>) do
  w flex-row: (
    for X in Projects do self project: X end
  );
end

command ps-welcome project: (Project is purr-project-meta) do
  w card: [    
    w button
      | title: "Open [Project name]" flatten-into-plain-text
      | on-click: { _ in purr open-project: Project }
      | with: [
          w card-image: [self project-icon: Project],
          w card-title: Project title,
          w card-description: Project description
        ]
  ];
end

command ps-welcome project-icon: (Project is purr-project-meta) do
  w icon: "box-open"
end