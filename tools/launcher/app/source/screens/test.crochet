% crochet

local define w = agata-widget;
local define cw = purr-common-widgets;

local abstract state;
local singleton not-started is state;
local type running(process is pp-test, results is cell<record>) is state;

command ps-test render do
  let State = #observable with-value: not-started;

  cw project-window: self.project at: "test" with: [
    w title: "Testing",
    w divider,

    w switch: State
      | when: {X in X is not-started} then: {_ in self not-started: State}
      | when: {X in X is running} then: {_ in self running: State},
  ];
end

command ps-test not-started: State do
  w container: [
    w toolbar: [
      w icon-button: "play"
        | title: "Run tests"
        | on-click: { _ in self run-tests: State }
    ],

    self test-results: State value
  ]
end

command ps-test running: State do
  w container: [
    w toolbar: [
      w icon: "spinner"
    ],

    self test-results: State value
  ]
end

command ps-test test-results: not-started do
  w container: [];
end

command ps-test test-results: (State is running) do
  w container: [
    w dynamic: { Canvas in
      let Handler = State.process.events subscribe: { Event in
        self handle-event: Event in: Canvas state: State;
      }
    }
  ];
end

command ps-test handle-event: (X is te-started) in: Canvas state: State do
end

command ps-test handle-event: (X is te-skipped) in: Canvas state: State do
  State add-result: X.result status: ts-skipped;
end

command ps-test handle-event: (X is te-failed) in: Canvas state: State do
  State add-result: X.result status: new ts-failed(X.message);
  Canvas append: (w flex-column: [
    w text: "[X.result.package-name] / [X.result.module-name]",
    w subtitle: X.result.title,
    w text: X.message,
    w divider,
  ])
end

command ps-test handle-event: (X is te-passed) in: Canvas state: State do
  State add-result: X.result status: ts-passed;
end

command ps-test handle-event: (X is te-finished) in: Canvas state: State do
  Canvas render: (self test-results: X.summary in: Canvas state: State);

end

command ps-test test-results: Summary in: Canvas state: State do
  w tabbed-panel: [
    w tab: "Summary"
      | with: [self test-result-summary: Summary],

    w tab: "Report"
      | with: [self test-result-report: State.results value],

    w tab: "Errors"
      | with: [self test-result-errors: State.results value],
  ]
end

command ps-test test-result-summary: Summary do
  w contents: [
    w flex-column: [
      w divider,
      w flex-row: [
        w text: "Passed: [Summary.passed to-text] / ",
        w text: "Failed: [Summary.failed to-text] / ",
        w text: "Skipped: [Summary.skipped to-text] / ",
        w text: "Total: [Summary.total to-text] / ",
        w text: "Duration: [Summary.duration milliseconds to-text]ms"
      ]
    ]
  ];
end

command ps-test test-result-report: Results do
  w contents: [
    w flex-column: (
      for Package-pair in Results pairs do
        let Package-name = Package-pair key;
        let Modules = Package-pair value;
        w flex-column: [
          w title: Package-name,
          w container: (
            for Module-pair in Modules pairs do
              let Module-name = Module-pair key;
              let Tests = Module-pair value;
              w flex-column: [
                w subtitle: Module-name,
                w container: (
                  for Test in Tests values do
                    self test-result-entry: Test
                  end
                ),
                w divider,
              ]
            end
          ),
          w double-space,
        ]
      end
    )
  ];
end

command ps-test test-result-errors: Results do
  w contents: [
    w flex-column: (
      for
        Modules in Results values,
        Tests in Modules values,
        Test in Tests values if Test.status is ts-failed
      do
        let Result = Test.result;
        w flex-column: [
          w text: "[Result.package-name] / [Result.module-name]",
          w subtitle: Result.title,
          w text: Test.status.message,
          w divider
        ];
      end
    )
  ]
end

command ps-test test-result-entry: Result do
  w flex-row: [
    self test-result-status: Result.status,
    w text: Result.result.title,
    w divider
  ]
end

command ps-test test-result-status: ts-started =
  w icon: "spinner";
command ps-test test-result-status: ts-passed =
  w icon: "check-circle";
command ps-test test-result-status: ts-skipped =
  w icon: "circle";
command ps-test test-result-status: ts-failed =
  w icon: "times-circle";

command ps-test run-tests: State do
  let Process = purr run-tests: self.project;
  State <- new running(Process, #cell with-value: [->]);
end

command running add-result: (Test is test-result) status: (Status is test-status) do
  let Results0 = self.results value;
  let Package0 = Results0 at: Test.package-name default: [->];
  let Module0 = Package0 at: Test.module-name default: [->];
  let Module1 = Module0 at: Test.test-id put: [
    result -> Test,
    status -> Status
  ];
  let Package1 = Package0 at: Test.module-name put: Module1;
  let Results1 = Results0 at: Test.package-name put: Package1;
  self.results <- Results1;
end