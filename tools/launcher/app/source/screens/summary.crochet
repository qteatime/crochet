% crochet

local define w = agata-widget;
local define cw = purr-common-widgets;

command ps-summary render do
  cw project-window: self.project at: "summary" with: [
    w title: self.project meta title,
    w paragraph: [
      w text: self.project meta description
    ],

    w section: [
      w tabbed-panel: [
        w tab: "Metadata"
          | with: [w space, self metadata],
        
        w tab: "Capabilities"
          | with: [w space, self capabilities],

        w tab: "Dependencies"
          | with: [w space, self dependencies],
      ]
    ]
  ];
end

command ps-summary metadata do
  w flex-column: [
    self group: "Package name"
         with: [w text: self.project meta name],

    self group: "Stability"
         with: [w text: self.project meta stability],

    self group: "Target platforms"
         with: [w text: self.project meta target]
  ]
end

command ps-summary capabilities do
  w flex-column: [
    w title: ["Required capabilities"],
    self capability-list: self.project meta capabilities required,

    w double-space,
    w title: ["Provided capabilities"],
    self capability-list: self.project meta capabilities provided,
  ]
end

command ps-summary capability-list: List do
  w flex-column: (
    for C in List do self capability: C end
  )
end

command ps-summary capability: C do
  w group: [
    w group-heading: (self capability-name: C),
    w container: [
      w text: C description
    ]
  ];
end

command ps-summary capability-name: (C is purr-valid-capability) =
  [C name];

command ps-summary capability-name: (C is purr-invalid-capability) =
  [C original-name, " (invalid)"];

command ps-summary dependencies do
  w flex-column: (
    for Dep in self.project meta dependencies do
      self dependency: Dep
    end
  )
end

command ps-summary dependency: Dep do
  w group: [
    w flex-row: [
      w group-heading: [w text: Dep name],
      w group-meta: [w text: Dep target],
    ],

    w flex-column: (
      for C in Dep capabilities do
        w container: [self capability: C]
      end
    )
  ]
end

command ps-summary group: Title with: Children do
  w group: [
    w group-heading: [w text: Title],
    w container: Children
  ];
end