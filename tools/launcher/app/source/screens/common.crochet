% crochet

local define w = agata-widget;

command purr-common-widgets window: (Top is list<widget>) with: (Children is list<widget>) do
  w window: ([
    w top-bar: ([
      w top-bar-panel: [
        w icon-button: "home"
          | on-click: { _ in purr home }
      ]
    ] ++ Top),
  ] ++ Children)
end

command purr-common-widgets project-menu: (Project is purr-project) at: (Section is text) do
  w flex-column: [
    w action-button
      | icon: "book"
      | title: "Summary"
      | description: "Basic information about the project"
      | on-click: { _ in purr project-summary: Project },

    w action-button
      | icon: "play"
      | title: "Run"
      | description: "Build and run the project"
      | on-click: { _ in purr project-run: Project },

    w action-button
      | icon: "vial"
      | title: "Test"
      | description: "Run all tests for the project"
      | on-click: { _ in purr open-test: Project },

    w action-button
      | icon: "archive"
      | title: "Package"
      | description: "Build a distributable package for the project"
      | on-click: { _ in purr project-packaging: Project },

    w action-button
      | icon: "gamepad"
      | title: "Playground"
      | description: "Interactively experiment with the project"
      | on-click: { _ in purr project-playground: Project },

    w action-button
      | icon: "book"
      | title: "Documentation"
      | description: "Reference documentation of the project"
      | on-click: { _ in purr project-documentation: Project },

    w action-button
      | icon: "folder-open"
      | title: "Open directory"
      | description: "Open the project directory in your OS"
      | on-click: { _ in purr-api launch-directory: Project },

    w action-button
      | icon: "laptop-code"
      | title: "Open in editor"
      | description: "Open the project in an external code editor"
      | on-click: { _ in purr-api launch-code-editor: Project },
  ]
  | layout
  | gap: 0 pixels
  | width: 400 pixels
  | flex-fixed-size;
end

command purr-common-widgets project-window: (Project is purr-project) at: Section with: (Children is list<widget>) do
  self window: [w title: Project meta title] with: [
    w section: [
      w flex-row: [
        self project-menu: Project at: Section,

        w contents: Children
          | layout
          | flex-grow
      ]
      | layout
      | flex-no-wrap
    ]
  ];
end