% crochet

command agata with-root: Root do: (Block is (() -> any)) do
  let Canvas = new dom-canvas(Root);
  handle
    Block();
  with
    on agata-presentation.show(Node) do
      Canvas render: Node;
      continue with nothing;
    end

    on agata-presentation.alert(Message) do
      foreign dom.alert(Message);
      continue with nothing;
    end
  end
end

command agata show: (Widget is widget) do
  let Node = dom-renderer render: Widget;
  perform agata-presentation.show(Node);
end

command agata window: (Url is text) =
  new dom-window(foreign dom.make-frame(Url));

command agata message-box: (Message is text) =
  perform agata-presentation.alert(Message);

command dom-dynamic-canvas render: (Widget is widget) do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.replace(Node);
end

command dom-dynamic-canvas append: (Widget is widget) do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.append(Node);
end

command dom-dynamic-canvas prepend: (Widget is widget) do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.prepend(Node);
end