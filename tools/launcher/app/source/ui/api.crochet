% crochet

local type modal-ref;

command agata with-root: Root do: (Block is (() -> A)) -> A do
  let Canvas = new dom-canvas(Root);
  let Modal-nodes = foreign dom.modal-container("agata-modal-top");  
  let Modal = new dom-modal(Modal-nodes.root, Modal-nodes.container, Modal-nodes.label);
  foreign dom.append-sibling(Root, Modal-nodes.root);
  let Modal-ref = #cell with-value: new modal-ref;
  // TODO: this needs to be a weak map
  let Mapping = #cell with-value: #map empty;
  handle
    Block();
  with
    on agata-presentation.show(Node) do
      Canvas render: Node;
      continue with nothing;
    end

    on agata-presentation.show-modal(Node) do
      let Ref = new modal-ref;
      Modal-ref <- Ref;
      Modal show: Node;
      continue with Ref;
    end

    on agata-presentation.hide-modal(Ref) do
      assert Modal-ref value =:= Ref;
      Modal-ref <- new modal-ref;
      Modal hide;
      continue with nothing;
    end

    on agata-presentation.alert(Message) do
      foreign dom.alert(Message);
      continue with nothing;
    end

    on agata-presentation.get-reference(Reference) do
      continue with Mapping value at: Reference;
    end

    on agata-presentation.register-reference(Reference, Node) do
      Mapping <- Mapping value at: Reference put: Node;
      continue with nothing;
    end
  end
end

command agata show: Widget do
  let Node = dom-renderer render: Widget;
  perform agata-presentation.show(Node);
end

command agata show-modal: Widget do
  let Node = dom-renderer render: Widget;
  perform agata-presentation.show-modal(Node);
end

command agata hide-modal: (Ref is modal-ref) do
  perform agata-presentation.hide-modal(Ref);
end

command agata window: (Url is text) =
  new dom-window(foreign dom.make-frame(Url));

command agata message-box: (Message is text) =
  perform agata-presentation.alert(Message);

command dom-dynamic-canvas render: Widget do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.replace(Node);
end

command dom-dynamic-canvas append: Widget do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.append(Node);
end

command dom-dynamic-canvas prepend: Widget do
  let Node = dom-renderer render: Widget;
  perform agata-canvas.prepend(Node);
end

command agata reference: (Name is text) =
  new agata-reference(Name);

command agata-reference materialisation =
  perform agata-presentation.get-reference(self);
