% crochet

type ljt-decoder(
  vm is ljt-vm,
  decoder is cell<binary-decoder>,
);

abstract ljt-decoder-error;
type ljt-decoder-error--decode-failed(state is ljt-decoder, error is any);

command ljt-vm decode: (View is byte-array-view) with: (Type is ljt-type) do
  let Decoder = new ljt-decoder(
    vm -> self,
    decoder -> #cell with-value: (#binary-decoder from: View),
  );

  Decoder decode-with: Type;
end

command ljt-vm decode: (Bytes is byte-array) with: Type =
  self decode: Bytes view with: Type;

command ljt-vm try-decode: Bytes with: Type =
  ljt try: {self decode: Bytes with: Type};

command ljt-decoder do-step: (F is (binary-decoder -> result<binary-decoder-next, error>)) type: (Type is ljt-type) do
  let Origin = self.decoder value;
  let Result = F(self.decoder value);
  condition
    when Result is ok do
      self.decoder <- Result value next;
      Result value value;
    end

    when Result is error =>
      #ljt-error raise: new ljt-error--decoding-failed(
        decoder -> Type,
        decoder-view -> Origin,
        reason -> decoding-failed-reason--couldnt-read-bytes,
        vm -> self.vm,
      );
  end
end

command ljt-decoder decode-with: t-bool =
  new v-bool(self do-step: (_ take-boolean) type: t-bool);

command ljt-decoder decode-with: t-int8 =
  new v-int8(self do-step: (_ take-integer-8bit) type: t-int8);

command ljt-decoder decode-with: t-int16 =
  new v-int16(self do-step: (_ take-integer-16bit) type: t-int16);

command ljt-decoder decode-with: t-int32 =
  new v-int32(self do-step: (_ take-integer-32bit) type: t-int32);

command ljt-decoder decode-with: t-uint8 =
  new v-uint8(self do-step: (_ take-unsigned-integer-8bit) type: t-uint8);

command ljt-decoder decode-with: t-uint16 =
  new v-uint16(self do-step: (_ take-unsigned-integer-16bit) type: t-uint16);

command ljt-decoder decode-with: t-uint32 =
  new v-uint32(self do-step: (_ take-unsigned-integer-32bit) type: t-uint32);

command ljt-decoder decode-with: t-float32 =
  new v-float32(self do-step: (_ take-float-32bit) type: t-float32);

command ljt-decoder decode-with: t-float64 =
  new v-float64(self do-step: (_ take-float-64bit) type: t-float64);

command ljt-decoder decode-with: t-integer =
  new v-integer(self do-step: (_ take-integer) type: t-integer);

command ljt-decoder decode-with: t-text =
  new v-text(self do-step: (_ take-text) type: t-text);

command ljt-decoder decode-with: (T is t-lazy) =
  self decode-with: (force T.thunk);

command ljt-decoder decode-with: (T is t-array) do
  let Size = self do-step: (_ take-unsigned-integer-32bit) type: t-uint32;
  condition
    when Size =:= 0 =>
      new v-array(T, []);

    otherwise do
      let Items = 
        for Index in 1 to: Size do
          self decode-with: T.item;
        end;
      new v-array(T, Items);
    end
  end
end

command ljt-decoder decode-with: (T is t-map) do
  let Size = self do-step: (_ take-unsigned-integer-32bit) type: t-uint32;
  condition
    when Size =:= 0 =>
      new v-map(T, #map empty);

    otherwise do
      let Entries =
        for Index in 1 to: Size do
          let Key = self decode-with: T.key;
          let Value = self decode-with: T.value;
          #association key: Key value: Value;
        end;
      let Map = #map from: Entries;
      new v-map(T, Map);
    end
  end
end

command ljt-decoder decode-with: (T is t-tuple) do
  let Values =
    for Type in T.types do
      self decode-with: Type;
    end;
  new v-tuple(T, Values);
end

command ljt-decoder decode-with: (T is t-tagged) do
  let Tag = self do-step: (_ take-unsigned-integer-32bit) type: t-uint32;
  not (T.tag =:= Tag) 
    | implies: {
        #ljt-error raise: new ljt-error--decoding-failed(
          decoder -> T,
          decoder-view -> self.decoder value,
          reason -> new decoding-failed-reason--unexpected-tag(
            tag -> Tag,
            expected -> T.tag,
          ),
          vm -> self.vm,
        );
      };

  let Value = self decode-with: T.typ;
  new v-tagged(T, Tag, Value);
end

command ljt-decoder decode-with: (T is t-choice) do
  let Tag = self do-step: (_ take-unsigned-integer-32bit) type: t-uint32;
  not (T.types contains-key: Tag)
    | implies: {
        #ljt-error raise: new ljt-error--decoding-failed(
          decoder -> T,
          decoder-view -> self.decoder value,
          reason -> new decoding-failed-reason--not-in-union(
            tag -> Tag,
            types -> T.types,
          ),
          vm -> self.vm,
        );
      };

  let Type = T.types at: Tag;
  let Value = self decode-with: Type.typ;
  new v-choice(T, new v-tagged(Type, Tag, Value));
end

command ljt-decoder decode-with: (T is t-optional) do
  let Has-value = self do-step: (_ take-boolean) type: t-bool;
  condition
    when Has-value do
      let Value = self decode-with: T.item;
      new v-just(T, Value);
    end

    otherwise => new v-none(T);
  end
end
