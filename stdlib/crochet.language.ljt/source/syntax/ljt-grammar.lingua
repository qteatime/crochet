type Module(info: Interval, schema: Schema, declarations: Declaration[])

type Schema(magic: Text, version: Integer)

type Declaration =
  | Type(info: Interval, name: Name, fields: Field[])
  | Union(info: Interval, name: Name, declarations: Declaration[])

type Field(info: Interval, name: Name, typ: Type_Expr)

type Type_Expr =
  | Named(info: Interval, name: Name)
  | List(info: Interval, typ: Type_Expr)
  | Map(info: Interval, key: Type_Expr, value: Type_Expr)
  | Optional(info: Interval, typ: Type_Expr)

type Name(name: Text)


grammar Ljt_Grammar : Module {
  Module =
    | header s:Schema xs:Declaration* end
      -> Module(meta, s, xs)

  Schema =
    | schema_ t:text version_ v:integer
      -> Schema(t, v)

  Declaration =
    | TypeDeclaration
    | UnionDeclaration

  TypeDeclaration =
    | type_ n:Name "{" xs:Field* "}"
      -> Declaration.Type(meta, n, xs)

  UnionDeclaration =
    | union_ n:Name "{" xs:TypeDeclaration* "}"
      -> Declaration.Union(meta, n, xs)

  Field =
    | field_ n:Name ":" t:Type_Expr ";"
      -> Field(meta, n, t)

  Type_Expr =
    | k:Type_Expr_List "->" v:Type_Expr_List
      -> Type_Expr.Map(meta, k, v)
    | Type_Expr_List

  Type_Expr_List =
    | t:Type_Expr "[]"
      -> Type_Expr.List(meta, t)
    | t:Type_Expr "?"
      -> Type_Expr.Optional(meta, t)
    | Type_Expr_Primary

  Type_Expr_Primary =
    | n:Name
      -> Type_Expr.Named(meta, n)
    | "(" t:Type_Expr ")"
      -> t

  Name =
    | n:name
      -> Name(n)

  // -- Lexical --
  token header (a file header) = space* "%" hs* "ljt/0" nl
  token hs = " " | "\t"
  token nl = "\r\n" | "\n" | "\r"
  token line = (~nl any)*
  token comment (a comment) = "//" line
  space += comment

  token name_start = "a".."z" | "A".."Z"
  token name_rest = "a".."z" | "A".."Z" | "-" | "0".."9"
  token name = name_start name_rest*


  token hex_digit = "0".."9" | "a".."f" | "A".."F"

  token text_character =
    | "\\" escape_sequence
    | ~("\"") any
  
  token escape_sequence =
    | "u" hex_digit hex_digit hex_digit hex_digit
    | "x" hex_digit hex_digit
    | any

  token t_text (a piece of text) =
    | "\"" text_character* "\""
  
  text =
    | x:t_text -> @parse_text(x)

  token dec_digit = "0".."9" | "_"

  token t_integer (an integer) =
    | ~"_" "-"? dec_digit+

  integer =
    | x:t_integer -> @parse_integer(x)

  kw<w> = x:w ~name_rest -> x

  type_ = kw<"type">
  union_ = kw<"union">
  field_ = kw<"field">
  schema_ = kw<"schema">
  version_ = kw<"version">

  reserved =
    | type_ | union_ | field_ | schema_ | version_
}