% crochet

/// A sequence that contains up to N first elements from the original one.
command (X has sequence, appendable-sequence, countable-container) take: (N is integer)
requires
  non-zero :: N >= 0
do
  let Go = { Xs, N in
    condition
      when Xs is-empty => [];
      when N =:= 0 => [];
      otherwise => Go(Xs rest, N - 1) prepend: Xs first;
    end
  };
  Go(X, N);
test
  assert ([1, 2, 3] take: 0) === [];
  assert ([1, 2, 3] take: 1) === [1];
  assert ([1, 2, 3] take: 2) === [1, 2];
  assert ([1, 2, 3] take: 3) === [1, 2, 3];
  assert ([1, 2, 3] take: 5) === [1, 2, 3];
end

/// A sequence that does not contain the first N elements from the original one.
command (X has sequence, appendable-sequence, countable-container) drop: (N is integer)
requires
  non-zero :: N >= 0
do
  let Go = { Xs, N in
    condition
      when Xs is-empty => [];
      when N =:= 0 => Xs;
      otherwise => Go(Xs rest, N - 1);
    end
  };
  Go(X, N);
test
  assert ([1, 2, 3] drop: 0) === [1, 2, 3];
  assert ([1, 2, 3] drop: 1) === [2, 3];
  assert ([1, 2, 3] drop: 2) === [3];
  assert ([1, 2, 3] drop: 3) === [];
  assert ([1, 2, 3] drop: 5) === [];
end


/// A sequence that contains elements from the first one up until the `Predicate`
/// stops holding.
command (X has sequence, appendable-sequence, countable-container) take-while: (Predicate is (A -> boolean)) do
  let Go = { Xs in
    condition
      when Xs is-empty => Xs;
      when Predicate(Xs first) => Go(Xs rest) prepend: Xs first;
      otherwise => Xs empty;
    end
  };
  Go(X);
test
  assert ([] take-while: (_ <= 2)) === [];
  assert ([1, 2, 3, 4] take-while: (_ <= 2)) === [1, 2];
  assert ([1, 2, 3, 2, 1] take-while: (_ <= 2)) === [1, 2];
end

/// A sequence that contains elements from when the [Predicate] starts holding
/// up to the last element.
command (X has sequence, countable-container) drop-while: (Predicate is (A -> boolean)) do
  let Go = { Xs in
    condition
      when Xs is-empty => Xs;
      when Predicate(Xs first) => Go(Xs rest);
      otherwise => Xs;
    end
  };
  Go(X);
test
  assert ([] drop-while: (_ <= 2)) === [];
  assert ([1, 2, 3, 4] drop-while: (_ <= 2)) === [3, 4];
  assert ([1, 2, 3, 2, 1] drop-while: (_ <= 2)) === [3, 2, 1];
end


/// Returns the first item, wrapped in [type:result], in the sequence for which
/// the predicate holds, or [type:not-found] if no items pass the predicate.
///
/// This function looks at as many items in the sequence as necessary to find
/// the first item, but no more than that. However, it can potentially span
/// the entire sequence, thus why it's limited to finite ones.
command (Xs has finite-sequence) find-first: (Predicate is (A -> boolean)) -> result<A, not-found> do
  let Go = { Xs in
    condition
      when Xs is-empty => #result error: not-found;
      when Predicate(Xs first) => perform non-local.result(Xs first);
      otherwise => Go(Xs rest);
    end
  };

  handle
    Go(Xs);
  with
    on non-local.result(X) => #result ok: X;
  end
test
  assert ([1, 2, 3] find-first: (_ > 1)) === (#result ok: 2);
  assert ([1, 2, 3] find-first: (_ > 3)) === (#result error: not-found);
end


/// Inserts `Separator` between each item of the sequence.
command (Xs has finite-sequence, countable-container, appendable-sequence, chainable-collection) intersperse: (Separator is A) do
  condition
    when Xs count <= 1 =>
      Xs;

    otherwise do
      let Prefix = Xs empty | append: Xs first;
      let Suffix = Xs rest flat-map: { X in Xs empty | append: Separator | append: X };
      Prefix ++ Suffix;
    end
  end
test
  assert ([] intersperse: 0) === [];
  assert ([1] intersperse: 0) === [1];
  assert ([1, 2, 3] intersperse: 0) === [1, 0, 2, 0, 3];
end


command (X has indexed-sequence) second =
  X at: 2;

command (X has indexed-sequence) third =
  X at: 3;

command (X has indexed-sequence) fourth =
  X at: 4;

command (X has indexed-sequence) fifth =
  X at: 5;

command (X has indexed-sequence) sixth =
  X at: 6;

command (X has indexed-sequence) seventh =
  X at: 7;

command (X has indexed-sequence) eight =
  X at: 8;

command (X has indexed-sequence) ninth =
  X at: 9;