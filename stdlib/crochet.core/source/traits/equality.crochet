% crochet

/// True if two values with an equality trait are not equal.
command (Left has equality) =/= (Right has equality) =
  not (Left === Right);


// # Boolean
command boolean === (That is boolean)
  = self =:= That
test
  assert true === true;
  assert false === false;
  assert not (true === false);
end

// # Enum


// # Text
command text === (That is text) = self =:= That;

// # Tuple
command tuple === (That is tuple) do
  condition
    when self count =/= That count => false;
    always =>
      self zip: That with: (_ === _) | all-true;
  end
end

// # Record
command record === (That is record) do
  self =:= That; // FIXME: implement this properly
end

// # Result
command result === result = false;
command ok === (That is ok) = self value === That value;
command error === (That is error) = self reason === That reason;

test "result === result" do
  assert (#result ok: 1) === (#result ok: 1);
  assert (#result error: 1) === (#result error: 1);
  assert not ((#result ok: 1) === (#result error: 1));
  assert not ((#result error: 1) === (#result ok: 1));
end