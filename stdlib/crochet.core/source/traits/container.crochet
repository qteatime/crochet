% crochet

/// True if the container has no elements.
command (X has countable-container) is-empty = X count === 0;

/// Updates the value associated with the key, if one exists. Otherwise
/// Associates the key with the default value.
command (X has modifiable-mapped-container) at: Key default: Default update: (Update is (A -> A)) do
  condition
    when self contains-key: Key => self at: Key put: Update(self at: Key);
    always => self at: Key put: Default;
  end
test
  assert ([->] at: "a" default: 1 update: (_ + 1)) === [a -> 1];
  assert ([a -> 1] at: "a" default: 1 update: (_ + 1)) === [a -> 2];
  assert ([a -> 1, b -> 2] at: "a" default: 1 update: (_ + 1)) === [a -> 2, b -> 2];
  assert ([a -> 1, b -> 2] at: "c" default: 1 update: (_ + 1)) === [a -> 1, b -> 2, c -> 1];
end