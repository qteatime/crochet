% crochet

// -- Map
implement mapped-container for map;

/// Returns the value that is associated with the given `Key`. The `Key`
/// must exist in the map.
command map at: Key
requires key-exists :: self contains-key: Key
do
  foreign map.at(self.box, Key);
test
  assert (#map empty | at: 1 put: 2 | at: 1) === 2;
  assert (#map empty | at: 1 put: 2 | at: 3 put: 4 | at: 3) === 4;
end

/// True if the given `Key` exists as a key in the map.
command map contains-key: Key -> boolean do
  foreign map.has(self.box, Key);
test
  assert not (#map empty | contains-key: 1);
  assert (#map empty | at: 1 put: 2 | contains-key: 1);
  assert (#map empty | at: 1 put: 2 | at: 3 put: 4 | contains-key: 3);
  assert not (#map empty | at: 1 put: 2 | at: 3 put: 4 | contains-key: 5);
end