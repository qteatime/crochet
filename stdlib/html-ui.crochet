% crochet

type html-element = foreign crochet.ui.html.element;
type html-menu = foreign crochet.ui.html.menu;

singleton html;

command html show: X =
  foreign crochet.ui.html.show(X);

command html wait-click =
  foreign crochet.ui.html.wait();

command html box: Name class: Class children: Children =
  foreign crochet.ui.html.box(Name, Class, Children);

command html text: Text =
  foreign crochet.ui.html.text(Text);

command html menu: Items class: Class =
  foreign crochet.ui.html.menu(Class, Items);

command (X is html-menu) selected =
  foreign crochet.ui.html.menu-selected(X);


command (X is text) show {
  html show: X text;
}

command (X is html-element) show {
  html show: X;
}

command X show-wait {
  X show;
  wait-click;
}

command (Children is stream) header {
  html
    box: "header"
    class: "crochet-header"
    children: Children;
}

command (Text is text) title {
  html
    box: "h1"
    class: "crochet-title"
    children: [html text: Text];
}

command (Text is text) subtitle {
  html
    box: "h2"
    class: "crochet-subtitle"
    children: [html text: Text];
}

command (Text is text) text {
  html text: Text;
}

command paragraph: (X is html-element) {
  html 
    box: "p"
    class: "crochet-paragraph"
    children: [X];
}

command flow: (Children is stream) {
  html
    box: "div"
    class: "crochet-flow"
    children: Children;
}

command stack: (Children is stream) {
  html
    box: "div"
    class: "crochet-stack"
    children: Children;
}

command emphasis: (X is html-element) {
  html
    box: "em"
    class: "crochet-emphasis"
    children: [X];
}

command strong: (X is html-element) {
  html
    box: "strong"
    class: "crochet-strong"
    children: [X];
}

command html divider {
  html
    box: "div"
    class: "crochet-divider"
    children: [];
}

command section: (Children is stream) {
  html
    box: "section"
    class: "crochet-section"
    children: Children;
}

command menu: (Items is stream) {
  html
    menu: Items
    class: "crochet-menu";
}