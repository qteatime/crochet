% crochet

type widget-tabbed-panel(
  tabs is list<tab>,
  layout is purr-variable<tabbed-panel-layout>,
  selected is purr-variable<any>
) is widget;

type tab(
  id is any,
  header is widget,
  content is widget
);

enum tabbed-panel-layout =
  horizontal-top,
  horizontal-bottom,
  vertical-left,
  vertical-right;


command #widget tabbed-panel: (Tabs is list) =
  new widget-tabbed-panel(
    tabs -> Tabs map: (package to-tab: _),
    layout -> #tabbed-panel-layout horizontal-top as purr-variable,
    selected -> nothing as purr-variable,
  );

command package to-tab: (X is tab) = X;

command widget-tabbed-panel selected: (X has to-purr-variable) =
  new widget-tabbed-panel(self with selected -> X as purr-variable);

command widget-tabbed-panel layout: (X has to-purr-variable) =
  new widget-tabbed-panel(self with layout -> X as purr-variable);


command #tab id: Id =
  new tab(
    id -> Id,
    header -> #widget space,
    content -> #widget space,
  );

command tab header: (X has to-widget) =
  new tab(self with header -> X as widget);

command tab content: (X has to-widget) =
  new tab(self with content -> X as widget);
