% crochet

type html-element = foreign html.element;
type html-menu = foreign html.menu;

singleton html;
singleton ui;

command html show: X =
  foreign html.show(X);

command html wait-click =
  foreign html.wait();

command html box: Name class: Class attributes: Attrs children: Children =
  foreign html.box(Name, Class, Attrs, Children);

command html text: Text =
  foreign html.text(Text);

command html menu: Items class: Class =
  foreign html.menu(Class, Items);

command (X is html-menu) selected =
  foreign html.menu-selected(X);

command html preload: Url =
  foreign html.preload(Url);

command html image: Url =
  foreign html.image(Url);

command html animate: Element frame-rate: Rate =
  foreign html.animate(Element, Rate);

command html make-animation: Elements =
  foreign html.make-animation(Elements);

command html mark =
  foreign html.mark();

command X show do
  X to-html show;
end

command (X is html-element) show do
  html show: X;
end

command X show-wait do
  X show;
  ui wait-click;
end

command ui wait-click do
  html wait-click;
end

command ui mark do
  html mark;
end

command X to-html do
  X debug-representation text;
end

command (X is html-element) to-html do
  X;
end

command (X is text) to-html do
  X text;
end

command (X is interpolation) to-html do
  X parts to-html;
end

command (X is thunk) to-html do
  (force X) to-html;
end

command (Xs is stream) to-html do
  let Items = for X in Xs do X to-html end;
  html box: "span"
       class: "crochet-stream"
       attributes: [->]
       children: Items;
end


command (Text is text) text do
  html text: Text;
end

command preload: (Url is text) do
  html preload: Url;
end

command html-element animate: (FrameRate is integer) do
  html animate: self frame-rate: FrameRate;
end

command image: (Url is text) do
  html box: "img"
       class: "crochet-image"
       attributes: [src -> Url]
       children: [];
end

command animation: (Elements is stream) do
  html make-animation: (for X in Elements do X to-html end);
end

command header: X do
  html
    box: "header"
    class: "crochet-header"
    attributes: [->]
    children: [X to-html];
end

command title: X do
  html
    box: "h1"
    class: "crochet-title"
    attributes: [->]
    children: [X to-html];
end

command subtitle: X do
  html
    box: "h2"
    class: "crochet-subtitle"
    attributes: [->]
    children: [X to-html];
end

command monospace: X do
  box: "crochet-mono" children: X;
end

command paragraph: X do
  html 
    box: "p"
    class: "crochet-paragraph"
    attributes: [->]
    children: [X to-html];
end

command box: (Class is text) children: X do
  html box: "div"
       class: Class
       attributes: [->]
       children: [X to-html];
end

command flow: X do
  box: "crochet-flow" children: X to-html;
end

command stack: X do
  box: "crochet-stack" children: X to-html;
end

command emphasis: X do
  html
    box: "em"
    class: "crochet-emphasis"
    attributes: [->]
    children: [X to-html];
end

command strong: X do
  html
    box: "strong"
    class: "crochet-strong"
    attributes: [->]
    children: [X to-html];
end

command ui divider do
  box: "crochet-divider" children: [];
end

command section: X do
  html
    box: "section"
    class: "crochet-section"
    attributes: [->]
    children: [X to-html];
end

command button: X do
  html
    box: "div"
    class: "crochet-button"
    attributes: [->]
    children: [X to-html];
end

command menu: (Items0 is stream) do
  let Items = for X in Items0 do
    [
      Title -> (button: X.Title to-html),
      Value -> X.Value,
    ]
  end;

  html
    menu: Items
    class: "crochet-menu";
end