% crochet

// -- Constructors
command #tuple empty = [];


// -- Containership
command (X is tuple) count =
  foreign tuple.count(X)
test
  assert [] count =:= 0;
  assert [1] count =:= 1;
  assert [1, 2, 3] count =:= 3;
end


command (X is tuple) is-empty =
  X count =:= 0
test
  assert [] is-empty;
  assert not ([0] is-empty);
end


command (X is tuple) contains: Value =
  foreign tuple.contains(X, Value)
test
  assert not ([] contains: 1);
  assert ([1] contains: 1);
  assert ([0, 1, 2] contains: 1);
  assert not ([0, 1, 2] contains: 3);
end


// -- Indexing and sequencing
command (X is tuple) at: (Index is integer)
requires within-bounds :: (Index >= 1) and (Index <= X count)
  = foreign tuple.at(X, Index)
test
  assert (["a", "b", "c"] at: 1) =:= "a";
  assert (["a", "b", "c"] at: 2) =:= "b";
  assert (["a", "b", "c"] at: 3) =:= "c";
end


command tuple first = self at: 1
test
  assert [1] first =:= 1;
  assert [1, 2, 3] first =:= 1;
end


command tuple second = self at: 2
test
  assert [1, 2] second =:= 2;
  assert [1, 2, 3] second =:= 2;
end


command tuple third = self at: 3
test
  assert [1, 2, 3] third === 3;
  assert [1, 2, 3, 4] third === 3;
end


command tuple last = self at: self count
test
  assert [1] last =:= 1;
  assert [1, 2, 3] last =:= 3;
end


command (X is tuple) at: (Index is integer) put: Value
requires within-bounds :: (Index >= 1) and (Index <= X count)
  = foreign tuple.at-put(X, Index, Value)
test
  assert (["a", "b", "c"] at: 1 put: "d") =:= ["d", "b", "c"];
  assert (["a", "b", "c"] at: 2 put: "d") =:= ["a", "d", "c"];
  assert (["a", "b", "c"] at: 3 put: "d") =:= ["a", "b", "d"];
end


command (X is tuple) delete-at: (Index is integer)
requires within-bounds :: (Index >= 1) and (Index <= X count)
  = foreign tuple.at-delete(X, Index)
test
  assert (["a"] delete-at: 1) =:= [];
  assert (["a", "b", "c"] delete-at: 2) =:= ["a", "c"];
  assert (["a", "b", "c"] delete-at: 3) =:= ["a", "b"];
end


command (X is tuple) at: (Index is integer) insert: Value
requires within-bounds :: (Index >= 1) and (Index <= X count)
  = foreign tuple.at-insert(X, Index, Value)
test
  assert (["a", "b", "c"] at: 1 insert: 0) =:= [0, "a", "b", "c"];
  assert (["a", "b", "c"] at: 2 insert: 0) =:= ["a", 0, "b", "c"];
  assert (["a", "b", "c"] at: 3 insert: 0) =:= ["a", "b", 0, "c"];
end


command (X is tuple) append: Value =
  X ++ [Value]
test
  assert ([] append: 1) === [1];
  assert ([0, 1, 2] append: 1) === [0, 1, 2, 1];
end


command (X is tuple) prepend: Value =
  [Value] ++ X
test
  assert ([] prepend: 1) === [1];
  assert ([0, 1, 2] prepend: 1) === [1, 0, 1, 2];
end


command (X is tuple) ++ (Y is tuple) = 
  foreign tuple.concat(X, Y)
test
  assert ([] ++ []) =:= [];
  assert ([1, 2, 3] ++ []) =:= [1, 2, 3];
  assert ([] ++ [1, 2, 3]) =:= [1, 2, 3];
  assert ([1, 2] ++ [3, 4, 5]) =:= [1, 2, 3, 4, 5];
end


command (X is tuple) slice-from: (Start is integer) to: (End is integer)
requires
  positive :: Start >= 1;
  progressive :: Start <= End;
  = foreign tuple.slice(X, Start, End)
test
  assert ([] slice-from: 1 to: 4) =:= [];
  assert ([0, 1, 2, 3] slice-from: 1 to: 4) =:= [0, 1, 2, 3];
  assert ([0, 1, 2, 3] slice-from: 1 to: 1) =:= [0];
  assert ([0, 1, 2, 3] slice-from: 4 to: 4) =:= [3];
  assert ([0, 1, 2, 3] slice-from: 2 to: 3) =:= [1, 2];
  assert ([0, 1, 2, 3] slice-from: 3 to: 5) =:= [2, 3];
end


command tuple slice-from: Start =
  self slice-from: Start to: (greater-of: Start and: self count)
test
  assert ([] slice-from: 1) === [];
  assert ([1] slice-from: 1) === [1];
  assert ([1, 2, 3] slice-from: 1) === [1, 2, 3];
  assert ([1, 2, 3] slice-from: 2) === [2, 3];
  assert ([1, 2, 3] slice-from: 3) === [3];
end


command tuple slice-to: End =
  self slice-from: 1 to: End
test
  assert ([] slice-to: 1) === [];
  assert ([1] slice-to: 1) === [1];
  assert ([1, 2, 3] slice-to: 1) === [1];
  assert ([1, 2, 3] slice-to: 2) === [1, 2];
  assert ([1, 2, 3] slice-to: 3) === [1, 2, 3];
end


command tuple but-first =
  condition
    when self count < 2 => [];
    always => self slice-from: 2 to: self count;
  end
test
  assert [] but-first =:= [];
  assert [1] but-first =:= [];
  assert [1, 2, 3] but-first =:= [2, 3];
end


command tuple but-last =
  condition
    when self count < 2 => [];
    always => self slice-from: 1 to: (self count - 1);
  end
test
  assert [] but-last =:= [];
  assert [1] but-last =:= [];
  assert [1, 2, 3] but-last =:= [1, 2];
end


command tuple take-while: (Fun is function-1) do
  let Go = { Xs in
    condition
      when Xs is-empty => [];
      when Fun(Xs first) => [Xs first] ++ Go(Xs but-first);
      always => [];
    end
  };
  Go(self);
test
  assert ([] take-while: (_ <= 2)) === [];
  assert ([1, 2, 3, 4] take-while: (_ <= 2)) === [1, 2];
  assert ([1, 2, 3, 2, 1] take-while: (_ <= 2)) === [1, 2];
end


command tuple drop-while: (Fun is function-1) do
  let Go = { Xs in
    condition
      when Xs is-empty => [];
      when Fun(Xs first) => Go(Xs but-first);
      always => Xs;
    end
  };
  Go(self);
test
  assert ([] drop-while: (_ <= 2)) === [];
  assert ([1, 2, 3, 4] drop-while: (_ <= 2)) === [3, 4];
  assert ([1, 2, 3, 2, 1] drop-while: (_ <= 2)) === [3, 2, 1];
end


command tuple enumerate =
  condition
    when self is-empty => [];
    always => (1 to: self count) zip: self with: { I, V in new indexed(I, V) };
  end
test
  assert ([] enumerate) === [];
  assert (["a", "b", "c"] enumerate)
    === [new indexed(1, "a"), new indexed(2, "b"), new indexed(3, "c")];
end


// -- Collection transformations
command tuple keep-if: (Fun is function-1) do
  for X in self if Fun(X) do X end
test
  assert ([] keep-if: (_ > 2)) === [];
  assert ([1, 2, 3, 2, 1] keep-if: (_ > 2)) === [3];
  assert ([1, 5, 3, 1, 6, 2] keep-if: (_ > 2)) === [5, 3, 6];
end


command tuple remove-if: (Fun is function-1) do
  for X in self if not Fun(X) do X end;
test
  assert ([] remove-if: (_ > 2)) === [];
  assert ([1, 2, 3, 2, 1] remove-if: (_ > 2)) === [1, 2, 2, 1];
  assert ([1, 5, 3, 1, 6, 2] remove-if: (_ > 2)) === [1, 1, 2];
end


command tuple map: (Fun is function-1) do
  foreign tuple.map(self, Fun);
test
  assert ([] map: (_ + 1)) === [];
  assert ([1] map: (_ + 1)) === [2];
  assert ([1, 2, 3, 4] map: (_ + 1)) === [2, 3, 4, 5];
end


command tuple flat-map: (Fun is function-1) do
  foreign tuple.flatmap(self, Fun);
test
  assert ([] flat-map: { A in [A, A] }) === [];
  assert ([1, 2, 3] flat-map: { A in [A, A] }) === [1, 1, 2, 2, 3, 3];
end


command tuple flatten-once do
  self flat-map: { X in X };
test
  assert ([] flatten-once) === [];
  assert ([[1], [2, 3], [4, [5], 6]] flatten-once) === [1, 2, 3, 4, [5], 6];
end


// -- Folding
command (X is tuple) fold-from: Initial with: (Fun is function-2) =
  foreign tuple.fold(X, Initial, Fun)
test
  assert ([1, 2, 3] fold-from: 0 with: (_ + _)) =:= (1 + 2 + 3 + 0);
  assert ([] fold-from: 0 with: (_ + _)) =:= 0;
  assert ([1] fold-from: 0 with: (_ + _)) =:= 1;
  assert ([1, 2, 3] fold-from: 6 with: (_ - _)) =:= (((6 - 1) - 2) - 3);
end


command (X is tuple) fold-right-from: Initial with: (Fun is function-2) =
  foreign tuple.foldr(X, Initial, Fun)
test
  assert ([] fold-right-from: 0 with: (_ + _)) =:= 0;
  assert ([1] fold-right-from: 0 with: (_ + _)) =:= 1;
  assert ([1, 2, 3] fold-right-from: 6 with: (_ - _)) =:= (1 - (2 - (3 - 6)));
end


command tuple fold-with: (Fun is function-2)
requires non-empty :: not (self is-empty)
do
  self but-first fold-from: self first with: Fun;
test
  assert ([1, 2, 3] fold-with: (_ + _)) =:= (1 + 2 + 3);
  assert ([1] fold-with: (_ + _)) =:= 1;
  assert ([1, 2, 3] fold-with: (_ - _)) =:= ((1 - 2) - 3);
end


command tuple fold-right-with: (Fun is function-2)
requires non-empty :: not (self is-empty)
do
  self but-last fold-right-from: self last with: Fun;
test
  assert ([1] fold-right-with: (_ + _)) =:= 1;
  assert ([1, 2, 3] fold-right-with: (_ - _)) =:= (1 - (2 - 3));
end


command tuple some: (Fun is function-1) do
  self fold-from: false with: { A, B in A or Fun(B) };
test
  assert ([] some: (_ === 2)) === false;
  assert ([1] some: (_ === 2)) === false;
  assert ([1, 2, 3] some: (_ === 2)) === true;
  assert ([1, 2, 3, 2, 1] some: (_ === 2)) === true;
  assert ([1, 3, 1, 3, 1] some: (_ === 2)) === false;
end


command tuple all: (Fun is function-1) do
  self fold-from: true with: { A, B in A and Fun(B) };
test
  assert ([] all: (_ > 3)) === true;
  assert ([1] all: (_ > 3)) === false;
  assert ([1, 2, 3] all: (_ > 3)) === false;
  assert ([4, 5, 6] all: (_ > 3)) === true;
end


command tuple all-true = self fold-from: true with: (_ and _)
test
  assert ([] all-true) === true;
  assert ([true, true] all-true) === true;
  assert ([false] all-true) === false;
  assert ([false, true, false] all-true) === false;
end


command tuple some-true = self fold-from: false with: (_ or _)
test
  assert ([] some-true) === false;
  assert ([true, true] some-true) === true;
  assert ([false] some-true) === false;
  assert ([false, true, false] some-true) === true;
end


command tuple sum = self fold-from: 0 with: (_ + _)
test
  assert ([] sum) === 0;
  assert ([1, 2, 3, 4] sum) === (0 + 1 + 2 + 3 + 4);
end


command tuple product = self fold-from: 1 with: (_ * _)
test
  assert ([] product) === 1;
  assert ([1, 2, 3, 4] product) === (1 * 1 * 2 * 3 * 4);
end


command tuple average -> float
requires non-empty :: not (self is-empty)
  = self sum / self count
test
  assert ([3] average) === 3.0;
  assert ([1, 2, 3, 4, 5, 6, 7] average) === 4.0;
  assert ([3, 5, 8, 1, 5] average) === 4.4;
end


command tuple maximum
requires non-empty :: not (self is-empty)
  = self fold-with: (greater-of: _ and: _)
test
  assert ([1] maximum) === 1;
  assert ([1, 0, 2] maximum) === 2;
  assert ([-3, 5, 2, -5] maximum) === 5;
end


command tuple minimum
requires non-empty :: not (self is-empty)
  = self fold-with: (lesser-of: _ and: _)
test
  assert ([1] minimum) === 1;
  assert ([1, 0, 2] minimum) === 0;
  assert ([-3, 5, 2, -5] minimum) === -5;
end


// -- Zipping
command (X is tuple) zip: (That is tuple) with: (Fun is function-2)
requires same-size :: self count === That count
  = foreign tuple.zip-with(X, That, Fun)
test
  assert ([1, 2, 3] zip: [4, 5, 6] with: (_ + _)) === [1 + 4, 2 + 5, 3 + 6];
end


command tuple zip: That = self zip: That with: { A, B in [A, B] }
test
  assert ([] zip: []) =:= [];
  assert ([1] zip: [2]) =:= [[1, 2]];
  assert ([1, 2, 3] zip: [4, 5, 6]) =:= [[1, 4], [2, 5], [3, 6]];
  assert ([1, 2] zip: ["a", "b"]) === [[1, "a"], [2, "b"]];
end


// -- Sorting
command (X is tuple) reverse
ensures same-size :: self count === return count
  = foreign tuple.reverse(X)
test
  assert [] reverse =:= [];
  assert [1] reverse =:= [1];
  assert [1, 2, 3] reverse =:= [3, 2, 1];
end


command tuple sort-by: (Comparator is function-2)
ensures same-size :: self count === return count do
  let Compare = { A, B in
    do
      let C = Comparator(A, B);
      condition
        when C === less-than => -1;
        when C === equal => 0;
        when C === greater-than => 1;
      end
    end
  };
  foreign tuple.sort(self, Compare)
test
  assert ([] sort-by: (_ compare-to: _)) =:= [];
  assert ([1] sort-by: (_ compare-to: _)) =:= [1];
  assert ([-2, 1, 0, 3] sort-by: (_ compare-to: _)) =:= [-2, 0, 1, 3];
end


command (X is tuple) sort
ensures same-size :: self count === return count
  = self sort-by: (_ compare-to: _)
test
  assert ([] sort) =:= [];
  assert ([1] sort) =:= [1];
  assert ([-2, 1, 0, 3] sort) =:= [-2, 0, 1, 3];
end


// -- Other
command (X is tuple) without-duplicates = foreign tuple.unique(X)
test
  assert ([] without-duplicates) === [];
  assert ([1, 2, 1, 3, 1, 4] without-duplicates) === [1, 2, 3, 4];
end
