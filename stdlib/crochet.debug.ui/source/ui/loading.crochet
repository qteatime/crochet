% crochet

open crochet.ui.agata;
open crochet.concurrency;

type loading-screen;

implement to-widget for loading-screen;
command loading-screen as widget do
  #widget flex-column: [
    #widget text: "Loading..."
  ]
  | align-items: #flex-align center
  | justify-content: #flex-justify center;
end


type loading-error-screen(error is text);

implement to-widget for loading-error-screen;
command loading-error-screen as widget do
  #widget flex-column: [
    #widget section: [
      #widget title: "Cannot initialise playground",
      #widget text: self.error
        | with-font: agata-theme-fonts monospace
        | with-text-color: agata-theme-colors red
        | with-white-space: #white-space pre-formatted-wrap-line
    ]
    | with-size: { S in S max-width: (80 as rem) }
    | with-padding: { P in P all: (1 as rem) }
  ]
  | align-items: #flex-align center
  | justify-content: #flex-justify center;
end