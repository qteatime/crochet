% crochet

singleton agata-dom;

command agata-dom with-root: (Root is unknown) do: (Block is (() -> A)) -> A do
  let Surface = dom make-surface-from-native-element: Root;
  Surface.node add-class: "agata-root";
  let References = #cell with-value: #map empty;
  handle
    Block();
  with
    on agata-presentation.show(Widget) do
      Surface replace-contents: Widget;
      continue with nothing;
    end

    on agata-rendering.get-shared-renderer() do
      continue with dom-renderer;
    end

    on agata-presentation.register-reference(Reference, Target) do
      References <- References value at: Reference put: Target;
      continue with nothing;
    end

    on agata-presentation.get-reference(Reference) do
      let Live = References value at: Reference;
      continue with Live;
    end
  end
end
