type Program(pos: Meta, declarations: Declaration[])

type Declaration =
  | Relation(pos: Meta, signature: Signature<RelationPart>)
  | Predicate(pos: Meta, signature: Signature<Name>, clauses: PredicateClause[])
  | Do(pos: Meta, body: Statement[])
  | ForeignCommand(pos: Meta, signature: Signature<Parameter>, body: FFI)
  | Command(pos: Meta, signature: Signature<Parameter>, body: Statement[])
  | Define(pos: Meta, name: Name, value: Expression)  // requires atomicity
  | Role(pos: Meta, name: Name)
  | SingletonType(pos: Meta, typ: TypeDef, init: TypeInit[])
  | Type(pos: Meta, typ: TypeDef)
  | Enum(pos: Meta, name: Name, values: Variant[])

type TypeDef(name: Name, roles: Name[])
type Variant(pos: Meta, name: Name, roles: Name[])
type FFI(pos: Meta, name: Namespace, args: Name[])

type TypeInit =
  | Fact(pos: Meta, sig: PartialSignature<Expression>)
  | Command(pos: Meta, sig: PartialSignature<Parameter>, body: Statement[])
  | ForeignCommand(pos: Meta, signature: PartialSignature<Parameter>, body: FFI)

type Parameter =
  | Untyped(pos: Meta, name: Name)
  | Typed(pos: Meta, name: Name, typ: TypeApp)
  | TypedOnly(pos: Meta, typ: TypeApp)

type TypeApp =
  | Union(pos: Meta, left: TypeApp, right: TypeApp)
  | Named(pos: Meta, name: Name)
  | Parens(pos: Meta, typ: TypeApp)

type PredicateClause(
  pos: Meta,
  predicate: Predicate,
  effect: PredicateEffect
)

type PredicateEffect =
  | Trivial()

type Statement =
  | Fact(pos: Meta, signature: Signature<Expression>)
  | Forget(pos: Meta, signature: Signature<Expression>)
  | Let(pos: Meta, name: Name, value: Expression)
  | Expr(value: Expression)

type Expression =
  | New(pos: Meta, typ: Name)
  | NewVariant(pos: Meta, typ: Name, variant: Name)
  | Invoke(pos: Meta, signature: Signature<Expression>)
  | Global(pos: Meta, name: Name)
  | Variable(pos: Meta, name: Name)
  | Self(pos: Meta)
  | List(pos: Meta, values: Expression[])
  | Record(pos: Meta, pairs: Pair<Name, Expression>[])
  | Cast(pos: Meta, typ: TypeApp, value: Expression)
  | Search(pos: Meta, predicate: Predicate)
  | Parens(pos: Meta, value: Expression)
  | Lit(value: Literal)

type Literal =
  | False(pos: Meta)
  | True(pos: Meta)
  | Text(pos: Meta, value: string)
  | Integer(pos: Meta, digits: string)

type Predicate(
  pos: Meta,
  relations: PredicateRelation[],
  constraint: Constraint
)

type PredicateRelation =
  | Not(pos: Meta, signature: Signature<Pattern>)
  | Has(pos: Meta, signature: Signature<Pattern>)

type Pattern =
  | HasRole(pos: Meta, typ: Name, name: Pattern)
  | HasType(pos: Meta, typ: TypeApp, name: Pattern)
  | Variant(pos: Meta, typ: Name, variant: Name)
  | Global(pos: Meta, name: Name)
  | Variable(pos: Meta, name: Name)
  | Wildcard(pos: Meta)
  | Lit(lit: Literal)

type Constraint =
  | And(pos: Meta, left: Constraint, right: Constraint)
  | Or(pos: Meta, left: Constraint, right: Constraint)
  | Not(pos: Meta, value: Constraint)
  | Equal(pos: Meta, left: Constraint, right: Constraint)
  | Variable(pos: Meta, name: Name)
  | Parens(pos: Meta, value: Constraint)
  | Lit(lit: Literal)

type Signature<T> =
  | Unary(pos: Meta, self: T, name: Name)
  | Binary(pos: Meta, op: Name, left: T, right: T)
  | Keyword(pos: Meta, self: T, pairs: Pair<Name, T>[])

type PartialSignature<T> =
  | Unary(pos: Meta, name: Name)
  | Binary(pos: Meta, op: Name, right: T)
  | Keyword(pos: Meta, pairs: Pair<Name, T>[])

type RelationPart =
  | Many(pos: Meta, name: Name)
  | One(pos: Meta, name: Name)

type Pair<K, V>(pos: Meta, key: K, value: V)
type Name(pos: Meta, name: string)
type Namespace(pos: Meta, names: Name[])


grammar Crochet : Program {
  program =
    | header ds:declaration* space* end
      -> Program(meta, ds)

  declaration =
    | relationDeclaration
    | predicateDeclaration
    | doDeclaration
    | commandDeclaration
    | roleDeclaration
    | typeDeclaration
    | enumDeclaration
    | defineDeclaration

  relationDeclaration =
    | relation_ s:logicSignature<relationPart> s<";">
      -> Declaration.Relation(meta, s)

  relationPart =
    | n:name s<"*"> -> RelationPart.Many(meta, n)
    | n:name        -> RelationPart.One(meta, n)

  predicateDeclaration =
    | predicate_ l:logicSignature<name> c:block<predicateClause>
      -> Declaration.Predicate(meta, l, c)

  predicateClause =
    | when_ p:predicate s<";">   -> PredicateClause(meta, p, PredicateEffect.Trivial())

  doDeclaration =
    | do_ xs:statementBlock<statement>
      -> Declaration.Do(meta, xs)

  commandDeclaration =
    | command_ s:signature<parameter> s<"="> b:foreignBody
      -> Declaration.ForeignCommand(meta, s, b)
    | command_ s:signature<parameter> b:statementBlock<statement>
      -> Declaration.Command(meta, s, b)

  foreignBody =
    | n:namespace s<"("> xs:listOf<name, s<",">> s<")"> s<";">
      -> FFI(meta, n, xs)

  parameter =
    | n:name                              -> Parameter.Untyped(meta, n)
    | s<"("> n:name is_ t:typeApp s<")">  -> Parameter.Typed(meta, n, t)
    | t:atom                              -> Parameter.TypedOnly(meta, TypeApp.Named(meta, t))

  typeApp =
    | typeAppUnion

  typeAppUnion =
    | l:typeAppPrimary s<"|"> r:typeAppUnion  -> TypeApp.Union(meta, l, r)
    | typeAppPrimary

  typeAppPrimary =
    | t:atom                    -> TypeApp.Named(meta, t)
    | s<"("> t:typeApp s<")">   -> TypeApp.Parens(meta, t)

  typeDeclaration =
    | singleton_ t:basicType i:typeInitBlock  -> Declaration.SingletonType(meta, t, i)
    | t:basicType s<";">                      -> Declaration.Type(meta, t)
  
  basicType =
    | type_ n:atom r:roles    -> TypeDef(n, r)

  typeInitBlock =
    | x:block<typeInit>   -> x
    | s<";">              -> []

  typeInit =
    | s:partialLogicSignature<invokePostfix> s<";">
      -> TypeInit.Fact(meta, s)
    | command_ s:partialSignature<parameter> s<"="> b:foreignBody
      -> TypeInit.ForeignCommand(meta, s, b)
    | command_ s:partialSignature<parameter> b:statementBlock<statement>
      -> TypeInit.Command(meta, s, b)

  roleDeclaration =
    | role_ n:atom s<";">           -> Declaration.Role(meta, n)

  enumDeclaration =
    | enum_ n:atom s<"="> s<"|">? vs:nonemptyListOf<variant, s<"|">> s<";">
      -> Declaration.Enum(meta, n, vs)

  variant =
    | n:atom r:roles  -> Variant(meta, n, r)

  roles =
    | s<"::"> r:nonemptyListOf<atom, s<",">>   -> r
    |                                          -> []

  defineDeclaration =
    | define_ n:atom s<"="> e:atomicExpression s<";">
      -> Declaration.Define(meta, n, e)

  // -- Logic
  predicate =
    | rs:predicateRelations if_ c:constraint    -> Predicate(meta, rs, c)
    | rs:predicateRelations                     -> Predicate(meta, rs, Constraint.Lit(Literal.True(meta)))

  predicateRelations =
    | nonemptyListOf<predicateRelation, s<",">>

  predicateRelation =
    | not_ s:logicSignature<pattern>  -> PredicateRelation.Not(meta, s)
    | s:logicSignature<pattern>       -> PredicateRelation.Has(meta, s)

  pattern =
    | s<"("> c:patternComplex s<")">  -> c
    | n:atom s<"."> v:atom    -> Pattern.Variant(meta, n, v)
    | n:atom                  -> Pattern.Global(meta, n)
    | l:literal               -> Pattern.Lit(l)
    | patternName

  patternComplex =
    | n:patternName is_ t:typeApp   -> Pattern.HasType(meta, t, n)
    | n:patternName s<"::"> t:atom  -> Pattern.HasRole(meta, t, n)

  patternName =
    | s<"_">        -> Pattern.Wildcard(meta)
    | n:name        -> Pattern.Variable(meta, n)

  constraint =
    | l:constraint and_ r:constraint  -> Constraint.And(meta, l, r)
    | l:constraint or_ r:constraint   -> Constraint.Or(meta, l, r)
    | constraint200

  constraint200 =
    | not_ c:constraint300            -> Constraint.Not(meta, c)
    | constraint300

  constraint300 =
    | l:constraint400 s<"==="> r:constraint400  -> Constraint.Equal(meta, l, r)
    | constraint400

  constraint400 =
    | n:name                          -> Constraint.Variable(meta, n)
    | l:literal                       -> Constraint.Lit(l)
    | s<"("> c:constraint s<")">      -> Constraint.Parens(meta, c)


  // -- Statement
  statement =
    | letStatement
    | factStatement
    | forgetStatement
    | e:expression    -> Statement.Expr(e)

  letStatement =
    | let_ n:name s<"="> e:expression
      -> Statement.Let(meta, n, e)
  
  factStatement =
    | fact_ s:logicSignature<invokePostfix> 
      -> Statement.Fact(meta, s)
  
  forgetStatement =
    | fact_ s:logicSignature<invokePostfix>
      -> Statement.Forget(meta, s)


  // -- Expressions
  expression =
    | searchExpression
    | invokeInfixExpression

  searchExpression =
    | search_ p:predicate
      -> Expression.Search(meta, p)

  invokeInfixExpression =
    | l:invokeMixfix op:infix_symbol r:invokeInfixExpression
      -> Expression.Invoke(meta, Signature.Binary(meta, op, l, r))
    | invokeMixfix

  invokeMixfix =
    | s:castExpression ps:signaturePair<invokePostfix>
      -> Expression.Invoke(meta, Signature.Keyword(meta, s, ps))
    | castExpression

  castExpression =
    | s:invokePostfix as_ t:typeAppPrimary
      -> Expression.Cast(meta, t, s)
    | invokePostfix

  invokePostfix =
    | s:invokePostfix n:atom
      -> Expression.Invoke(meta, Signature.Unary(meta, s, n))
    | primaryExpression

  primaryExpression =
    | newExpression
    | literalExpression
    | recordExpression<expression>
    | listExpression<expression>
    | self_                         -> Expression.Self(meta)
    | n:atom                        -> Expression.Global(meta, n)
    | n:name                        -> Expression.Variable(meta, n)
    | s<"("> e:expression s<")">    -> Expression.Parens(meta, e)

  newExpression =
    | new_ n:atom            -> Expression.New(meta, n)
    | n:atom s<"."> v:atom   -> Expression.NewVariant(meta, n, v)

  listExpression<e> =
    | s<"["> xs:listOf<e, s<",">> s<",">? s<"]">
      -> Expression.List(meta, xs)

  recordExpression<e> =
    | s<"["> s<"->"> s<"]">                                             -> Expression.Record(meta, [])
    | s<"["> xs:nonemptyListOf<recordPair<e>, s<",">> s<",">? s<"]">    -> Expression.Record(meta, xs)

  recordPair<e> =
    | n:name s<"->"> v:e -> Pair(meta, n, v)

  literalExpression =
    | l:literal             -> Expression.Lit(l)

  atomicExpression =
    | newExpression
    | literalExpression
    | recordExpression<atomicExpression>
    | listExpression<atomicExpression>

  // -- Literals
  literal =
    | text
    | integer
    | boolean

  boolean =
    | true_                 -> Literal.True(meta)
    | false_                -> Literal.False(meta)

  text =
    | x:s<t_text>           -> Literal.Text(meta, x)

  integer =
    | x:s<t_integer>        -> Literal.Integer(meta, x)

  // -- Token -> Rule & Basics
  atom = ~reserved x:s<t_atom> ~":"           -> Name(meta, x)
  name = x:s<t_name>                          -> Name(meta, x)
  keyword = x:s<t_keyword>                    -> Name(meta, x)
  infix_symbol = x:s<t_infix_symbol>          -> Name(meta, x)
  not = x:not_                                -> Name(meta, x)

  namespace = x:nonemptyListOf<atom, s<".">>  -> Namespace(meta, x)

  // -- Higher-order
  logicSignature<t> =
    | s:t kws:signaturePair<t>+       -> Signature.Keyword(meta, s, kws)
    | s:t n:atom                      -> Signature.Unary(meta, s, n)

  signaturePair<t> =
    | kw:keyword v:t  -> Pair(meta, kw, v)

  partialLogicSignature<t> =
    | kws:signaturePair<t>+       -> PartialSignature.Keyword(meta, kws)
    | n:atom                      -> PartialSignature.Unary(meta, n)

  partialSignature<t> =
    | kws:signaturePair<t>+       -> PartialSignature.Keyword(meta, kws)
    | op:infix_symbol r:t         -> PartialSignature.Binary(meta, op, r)
    | n:atom                      -> PartialSignature.Unary(meta, n)
    | n:not                       -> PartialSignature.Unary(meta, n)

  signature<t> =
    | s:t kws:signaturePair<t>+       -> Signature.Keyword(meta, s, kws)
    | l:t op:infix_symbol r:t         -> Signature.Binary(meta, op, l, r)
    | s:t n:atom                      -> Signature.Unary(meta, s, n)
    | n:not s:t                       -> Signature.Unary(meta, s, n)

  statementBlock<t> =
    | s<"{"> xs:listOf<t, s<";">> s<";">? s<"}"> -> xs

  block<t> =
    | s<"{"> xs:t* s<"}"> -> xs

  s<p> = space* x:p -> x


  // -- Lexical
  token header (a file header) = space* "%" hs* "crochet" nl
  token hs = " " | "\t"
  token nl = "\n" | "\r"
  token line = (~nl any)*
  token comment (a comment) = "//" line
  space += comment

  token atom_start = "a".."z"
  token atom_rest = letter | digit | "-"
  token t_atom (an atom) = atom_start atom_rest*

  token t_keyword (a keyword) = t_atom ":"

  token name_start = "A".."Z" | "_"
  token name_rest = letter | digit | "-"
  token t_name (a name) = name_start name_rest*

  token t_infix_symbol =
    | "+" | "-" | "*" | "/"
    | "<" | ">" | "<=" | ">="
    | "===" | "=/="
    | and_ | or_

  // -- Literals
  token dec_digit = "0".."9" | "_"

  token t_integer (an integer) =
    | ~"_" dec_digit+

  token text_character =
    | "\\" "\""
    | ~"\"" any

  token t_text (a text) =
    | "\"" text_character* "\""


  // -- Reserved words
  kw<w> = s<w> ~atom_rest

  relation_ = kw<"relation">
  predicate_ = kw<"predicate">
  when_ = kw<"when">
  do_ = kw<"do">
  command_ = kw<"command">
  type_ = kw<"type">
  role_ = kw<"role">
  enum_ = kw<"enum">
  define_ = kw<"define">
  singleton_ = kw<"singleton">

  let_ = kw<"let">
  return_ = kw<"return">
  fact_ = kw<"fact">
  forget_ = kw<"forget">
  new_ = kw<"new">
  search_ = kw<"search">
  if_ = kw<"if">

  true_ = kw<"true">
  false_ = kw<"false">
  not_ = kw<"not">
  and_ = kw<"and">
  or_ = kw<"or">
  is_ = kw<"is">
  self_ = kw<"self">
  as_ = kw<"as">

  reserved =
    | relation_ | predicate_ | when_ | do_ | command_
    | type_ | role_ | enum_ | define_ | singleton_
    | let_ | return_ | fact_ | forget_ | new_ | search_ | if_
    | true_ | false_ | not_ | and_ | or_ | is_ | self_ | as_
}