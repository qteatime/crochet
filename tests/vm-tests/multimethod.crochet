% crochet

trait spice;
trait pain;
implement spice for float;
implement pain for float;
implement pain for text;

command any multimethod-test-other = "any other";
command any multimethod-test = "any test";
command (_ has spice, pain) multimethod-test = "any+spice,pain test";
command integer multimethod-test = "integer test";
command function multimethod-test = "function test";
command boolean multimethod-test = "boolean test";
command true multimethod-test = "true test";
command #integer multimethod-test = "#integer test";
command (_ is float has pain) multimethod-test-other = "float+pain other";
command float multimethod-test-other = "float other";
command (_ is fractional has pain) multimethod-test-other = "fractional+pain other";
command (_ has pain) multimethod-test-other = "any+pain other";

test "Multimethod selection and dispatch" do
  // Only subtyping
  assert 1 multimethod-test-other =:= "any other";
  assert 1 multimethod-test =:= "integer test";
  assert (_ + 1) multimethod-test =:= "function test";
  assert true multimethod-test =:= "true test";
  assert false multimethod-test =:= "boolean test";
  assert #integer multimethod-test =:= "#integer test";
  assert #integral multimethod-test =:= "any test";

  // Subtyping + traits
  assert 1.0 multimethod-test =:= "any+spice,pain test";
  assert 1.0 multimethod-test-other =:= "float+pain other";
  assert "a" multimethod-test-other =:= "any+pain other";
end