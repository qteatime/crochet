% crochet

abstract arithmetic;

command #arithmetic evaluate: (X is skeleton-node) do
  condition
    when X name =:= "add _ _" do
      let Left = X children at: 1;
      let Right = X children at: 2;
      (self evaluate: Left) + (self evaluate: Right);
    end
      
    when X name =:= "sub _ _" do
      let Left = X children at: 1;
      let Right = X children at: 2;
      (self evaluate: Left) - (self evaluate: Right);
    end

    when X name =:= "display _" do
      [ value -> self evaluate: (X children at: 1),
        tag -> self evaluate: (X attributes).tag
      ]
    end

    when X name =:= "list _" do
      self evaluate: (X children at: 1);
    end
  end
end

command #arithmetic evaluate: (Xs is list) do
  for X in Xs do self evaluate: X end
end

command #arithmetic evaluate: (X is skeleton-literal) = X value;
command #arithmetic evaluate: (X is skeleton-dynamic) = (X expression)();

command #arithmetic evaluate: (X is skeleton-list) =
  self evaluate: X children;

command #arithmetic evaluate: (X is skeleton-interpolation) do
  let Parts =
    for P in X parts do
      condition
        when P is static-text => P;
        when P is skeleton-ast => self evaluate: P;
      end
    end;
  
  Parts as interpolation;
end


test "Simple evaluation" do
  let Result =
    dsl arithmetic with
      add 1 (add 2 (sub 3 2))
    end;

  assert Result =:= [1 + (2 + (3 - 2))];
end

test "Evaluation with thunks" do
  let Result =
    dsl arithmetic with
      add 1 (add 2 @(3 - 2))
    end;

  assert Result =:= [1 + (2 + (3 - 2))];
end

test "Attributes" do
  let Result =
    dsl arithmetic with
      display (add 1 2) --tag "1 + 2";
      display (add 3 4) --tag "3 + 4";
    end;

  assert Result =:= [
    [value -> 1 + 2, tag -> "1 + 2"],
    [value -> 3 + 4, tag -> "3 + 4"]
  ]
end

test "List children" do
  let Result =
    dsl arithmetic with
      list [1; 2; 3; 4]
    end;

  assert Result =:= [
    [1, 2, 3, 4]
  ];
end

open crochet.debug;

test "Interpolations" do
  let Result =
    dsl arithmetic with
      display <<Hello, [list [1; 2]] to [add 1 2]>> --tag "a";
    end;

  assert Result =:= [
    [
      value -> "["Hello, "][[1, 2]][" to "][1 + 2]",
      tag -> "a"
    ]
  ];
end