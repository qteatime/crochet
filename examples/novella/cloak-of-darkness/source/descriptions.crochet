% crochet

open crochet.novella;

command Room description do
  let Descriptions =
    match
      when Room has-light, 
          cloak on: player,
          Room dark-description: Description
      =>
        Description;

      when Room is-locked, Room locked-description: Description =>
        Description;

      when Room description: Description =>
        Description;
    end;
  Descriptions first;
end

command Room background do
  let Bg =
    match
      when Room has-light,
           cloak on: player =>
        nothing;

      when Room background: Asset =>
        Asset;

      otherwise =>
        nothing;
    end;

  condition
    when Bg first is nothing => nothing;
    otherwise => #nvl-image from-asset: Bg first;
  end
end

command Thing name do
  ((search Thing name: Name) first).Name;
end

command Thing short-name do
  ((search Thing short-name: Name) first).Name;
end