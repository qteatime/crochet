% crochet

/// An unique identifier for each todo item. Not committed to any serialisation.
type todo-id;

/// A single todo item
type todo-item(id is todo-id, title is text, done is boolean);

/// A list of todo items
type todo(items is list<todo-item>);


/// Adds a todo item
command todo add: (Item is todo-item) =
  new todo(self.items append: Item);

command todo remove: (Item is todo-id) =
  new todo(self.items keep-if: { X in not (X.id =:= Item) });

command todo mark: (Id is todo-id) done: (Done is boolean) =
  new todo(self.items map: { Item in
    condition
      when Item.id =:= Id => Item done: Done;
      otherwise => Item;
    end
  });

/// Creates a todo item
command #todo-item with-title: (Title is text) =
  new todo-item(new todo-id, Title, false);

command #todo-item with-title: (Title is interpolation) =
  new todo-item(new todo-id, Title flatten-into-plain-text, false);

command todo-item done: (Done is boolean) =
  new todo-item(self with done -> Done);
