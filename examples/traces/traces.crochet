% crochet

command wait-click = wait-click();

command page {
  wait-click;
  new-page;
}


actor #dear;

relation Person* pronouns She Her Hers;
relation Infinitive* for Pronoun* conjugated Conjugated;

actor #prologue;
actor #main;

relation Chapter chapter;
relation turn Number;
relation flag Flag*;

actor #flag-looked-around;
actor #flag-can-go-outside;

do {
  fact "was" for "they" conjugated "were";

  goto prologue;
}

command Form conjugate {
  return
    match {
      when
        #dear pronouns Pronoun _ _,
        Inf for Pronoun conjugated Verb
        if Inf === Form
      {
        return Verb;
      }

      else {
        return Form;
      }
    };
}

command she {
  return
    match {
      when #dear pronouns P _ _ {
        return P;
      }
    };
}

command her {
  return
    match {
      when #dear pronouns _ P _ {
        return P;
      }
    };
}

command hers {
  return
    match {
      when #dear pronouns _ _ P {
        return P;
      }
    };
}

scene prologue {
  fact #prologue chapter;

  "Traces" title show;

  say:
    "You had no memory of how all of this started. All you knew at that point
    was that you were alive. And that, at some point, you had met someone very,
    very dear to you.";

  trigger action;

  say:
    "[she title-case] ["was" conjugate] very dear to you.";

  wait-click;

  say:
    "["was" conjugate title-case].";

  page;

  say:
    "In the dimly lit cabin, your mind tried reminiscing the days
    [she] ["was" conjugate] with you. In a far away place.
    A long, long time ago.";
  
  wait-click;

  say:
    "In vain.";

  say:
    "All you knew at that point was that you were alive. And that you
    had met [her]. And that [she] ["was" conjugate] very dear to you.";

  say:
    "You didn't know why. Or how.";

  page;

  say:
    "There was a certain point in the day---you didn't keep track of the
    hours anymore, time remained constant in the cabin. There was a certain
    point in the day where you would get up from the sofa and go through
    all of the things laying about.";

  say:
    "Maybe, this time, you could piece things together in a way that made sense.";

  goto main;
}

action "He was very kind..."
:: sticky
when #prologue chapter {
  say:
    "Yes, he was a very kind person. Of that you were fairly certain.
    Although no examples of what his kindness meant came to mind at
    the moment.";

  fact #dear pronouns "he" "him" "his";
}

action "Her smile was what kept me going..."
:: sticky
when #prologue chapter {
  say:
    "You had this feeling that the reason you were still here today was
    because of her. You weren't quite sure how, or why, but her sweet
    smile wouldn't leave your mind.";
  
  fact #dear pronouns "she" "her" "hers";
}

action "They smelled like strawberries..."
:: sticky
when #prologue chapter {
  say:
    "Strawberries! Yes, that's the scent that wouldn't leave your mind.
    You weren't quite sure which cosmetics they used, but even now the
    thought of that smell alone was enough to make you relax.";

  fact #dear pronouns "they" "them" "their";
}

//== Main
scene main {
  fact #main chapter;
  fact turn 0;
  goto main-loop;
}

scene main-loop {
  trigger action;
  trigger next-turn;
  goto main-loop;
}

context next-turn {
  when turn Number {
    fact turn (Number + 1);
  }
}

scene ending {
  say: "(The end)";
}

action "Go outside"
when
  #main chapter
{
  say:
    "You turn the doorknob. The door opens.";

  say:
    "You pull the door slowly and take a peek outside.";

  say:
    "What awaits you is a sea of whiteness. The old log cabin seems to float
    in the void. There's nowhere you could escape to, if you wanted.";

  say:
    "Still, you find this sensation surprisingly comforting.";

  say:
    "Your feet carries you outside before you could think twice. Though your
    feet isn't touching anything, you don't fall. Nothing seems to pull you
    downwards.";

  say:
    "\"Ah...\"";
  
  say:
    "You remember now. Not everything. Not even most things. But you remember
    enough.";

  say:
    "You don't know how it all started. All you knew at that point was that
    you had met someone very, very dear to you. And that you loved [her] with
    all your heart. And that [she] ["was" conjugate] alive.";

  goto ending;
}

action "Look around"
when
  #main chapter,
  not flag #flag-looked-around
{
  say:
    "The old log cabin is much too dim to see things clearly. But by now your
    eyes were already used to the darkness. You could navigate the small room
    on your instincts alone.";

  say:
    "Behind you there was a large sofa. You had been spending copious amounts
    of time on it. Doing nothing in particular.";

  say:
    "In front of the sofa there was a fire-place. Although there was no wood
    to burn, it wasn't so cold inside that you would *need* it to keep yourself
    warm.";

  say:
    "To your right, the entire wall was filled by a bookshelf. Opposite of it,
    a small bar divided the living room and the small kitchen.";

  fact flag #flag-looked-around;
}

action "Examine bookshelf"
when
  #main chapter,
  flag #flag-looked-around
{

}

action "Examine bar"
when
  #main chapter,
  flag #flag-looked-around
{

}

action "Examine kitchen"
when
  #main chapter,
  flag #flag-looked-around
{

}