% crochet

open crochet.concurrency;
open crochet.ui.agata;

command todo-ui render-list: (List is todo-list) do
  let Items = self events items-changed: List.id
                |> _ map: { X in X.items }
                |> #observable-cell from-stream: _ initial-value: List all-items;
  let New-item = #observable-cell with-value: "";
  let Key-events = #event-stream empty;
  Key-events
    | keep-if: { X in X key =:= key-code-enter }
    | listener
    | subscribe: { _ in
        let Title = New-item value;
        New-item <- "";
        self add: (#todo-item with-title: Title) to: List.id;
      } capture;

  #widget flex-column: [
    #widget page-title: List name,

    #widget space | vertical: (2 as rem),

    #flex-child fluid: [
      self render-list-items: Items
    ],

    #widget space | vertical: (2 as rem),

    #widget card: [
      #card-child body: [
        #widget flex-row: [
          #widget icon: "plus",
          #widget text-input
            | placeholder: "Add an item"
            | value: New-item
            | key-up: Key-events
        ]
        | gap: (1 as rem)
        | align-items: "center"
      ]
    ]
  ]
end

command todo-ui render-list-items: (Items is observable-cell<list<todo-item>>) do
  Items map: { Xs in
    #widget flex-column: (Xs map: (self render-item: _))
      | gap: (0.5 as rem)
  };
end