% crochet

open crochet.concurrency;
open crochet.ui.agata;
open crochet.time;

singleton todo-app;

command main-html: Root do
  let Next-id = #cell with-value: 1;
  handle
    let Ui = #todo-ui empty;
    let List = todo-app example-list;
    Ui add-list: List;
    Ui add-list: (#todo-list-concrete with-name: "Habits");

    agata register-page: Ui;
    agata navigate: (Ui uri-pattern instantiate: [id -> List.id to-text]);
    
    agata-dom initialise;
  with
    use agata-dom-renderer root: Root;

    on todo-item.unique-id() do
      let Id = Next-id value;
      Next-id <- Id + 1;
      continue with (#todo-id from: Id to-text);
    end
  end
end

command todo-app example-list do
  #todo-list-concrete with-name: "To-do"
    | add: (#todo-item with-title: "Something")
    | add: (#todo-item with-title: "Some other thing")
    | add: (#todo-item with-title: "More things even");
end